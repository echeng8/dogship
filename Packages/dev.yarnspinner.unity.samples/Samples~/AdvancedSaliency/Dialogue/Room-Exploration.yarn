// the exploration scenario
// in this scenario primary and secondary are working together to explore the room

title: Primary
when: $scenario == .Explore
when: $scenario_state == .NotStarted
---
Player: ok time to wander around aimlessly looking for stuff
{$primary}: Great idea
Player: ok, any idea where to look?
{$primary}: I think {$secondary} had some ideas
Player: gotcha

<<set $scenario_state = .Started>>
===

title: Secondary
when: $scenario == .Explore
when: $scenario_state == .NotStarted
---
{$secondary}: Oh hey, we are exploring a {$Room}.
===


// once started you will need to speak to secondary and then primary in that order to complete the scenario
title: Primary
when: $scenario == .Explore
when: $scenario_state == .Started
when: $speak_to_secondary == true
---
{$primary}: How's the exploration go?
Player: I spoke to {$secondary}
{$primary}: oh?
Player: Yeah, they said there is nothing to find here.
{$primary}: oh, that's a bit of a bummer
Player: yeah it is.
{$primary}: sigh

<<set $scenario_state = .Complete>>
===

// catches the situation where you just keep talking to primary
title: Primary
when: $scenario == .Explore
when: $scenario_state == .Started
when: $speak_to_secondary == false
---
{$primary}: Asked {$secondary} if they have any ideas yet?
Player: getting to it
{$primary}: ok, keep me in the loop please.
Player: will do.
===

title: Secondary
when: $scenario == .Explore
when: $scenario_state == .Started
when: $speak_to_secondary == false
---
Player: so {$primary} said you had some ideas about exploring?
{$secondary}: totally
Player: care to share them?
{$secondary}: yeah so because we are in a {$Room} I reckon there is nothing to find.
Player: Oh
{$secondary}: yeah, should prolly let {$primary} know

<<set $speak_to_secondary = true>>
===

// catches the situation where you just keep talking to secondary
title: Secondary
when: $scenario == .Explore
when: $scenario_state == .Started
when: $speak_to_secondary == true
---
{$secondary}: Have you told {$primary} there is nothing to do here?
Player: I am getting to it.
{$secondary}: righto
===




// the scenario complete infinite lines

title: Primary
when: $scenario == .Explore
when: $scenario_state == .Complete
---
{$primary}: I think it's sad there's this whole place and nothing to explore in it
Player: yeah
===
title: Secondary
when: $scenario == .Explore
when: $scenario_state == .Complete
---
// showing off some linegroup based variance

=> {$secondary}: I can't believe there wasn't any butler murderings going on <<if $Room == .Mansion>>
=> {$secondary}: This place has genuine 19th century furniture <<if $Room == .Mansion>>
=> {$secondary}: not even a single secret cult message <<if $Room == .Church>>
=> {$secondary}: damn nice edifice though <<if $Room == .Church>>
=> {$secondary}: in hindsight a Pub has few secrets <<if $Room == .Pub>>
=> {$secondary}: at least we can grab a pint <<if $Room == .Pub>>
=> {$secondary}: honestly I don't even know why you'd explore an office <<if $Room == .Office>>
=> {$secondary}: I wonder if there are any paperclips, my maximiser is asking for more <<if $Room == .Office>>

Player: so wise
===
