// the Interogation scenario nodes
// this tells the story of primary and the player working together to interrogate secondary

title: Primary
when: $scenario == .Interogation
when: $scenario_state == .NotStarted
---
{$primary}: ok you be good cop, I'll be bad
Player: yep

<<set $scenario_state = .Started>>
===

// in the specific situation where alice is the primary we have her give a little speach before the rest of the normal flow takes over
title: Alice
when: $primary == .Alice
when: $scenario == .Interogation
when: $scenario_state == .NotStarted
when: once
---
Alice: So it's time to break {$secondary}.
Player: oh?
Alice: yeah they are real twisted and I will need your help
Player: oh cool
Alice: agreed, anyways, talk to me again please and we'll discuss strategy.
===

// secondary has little to play in this scenario so we have a very generic fallback node here
title: Secondary
when: $scenario == .Interogation
---
{$secondary}: Why are you talking to me?
===






// once the scenario has started it plays out the same
// the players brilliant interrogation technique the scenario ends
title: Primary
when: $scenario == .Interogation
when: $scenario_state == .Started
---
{$primary}: you are in real trouble now
{$secondary}: Oh am I now!?

// quick demonstration here of using linegroups to inject even more variance into the story without also needing to make entire new storylets
=> Player: it's weird we're in a mansion for this right? <<if $Room == .Mansion>>
=> Player: anyone fancy a pint? <<if $Room == .Pub>>
=> Player: this feels like the right place for this <<if $Room == .Office>>
=> Player: I reckon the symbolism here is confusing <<if $Room == .Church>>

{$primary}: quiet for a second please

Player: want to go see Les Mis?
{$secondary}: huh?
{$primary}: Yeah, what?
Player: anyone fancy an icecream?, I could go pick us some up.
{$secondary}: I confess, just stop being so weird.

<<set $scenario_state = .Complete>>
===







// post scenario chatter
title: Primary
when: $scenario == .Interogation
when: $scenario_state == .Complete
---
{$primary}: look I am not sure what you were doing but wow did it ever work.
Players: thanks
===

title: Secondary
when: $scenario == .Interogation
when: $scenario_state == .Complete
---
// again line groups to inject more variance into the story
=> {$secondary}: I promise I'll be good, please don't Henry the Train me.
=> {$secondary}: I'm done hiding the corpses of my masters victims. <<if $Room == .Mansion>>
=> {$secondary}: I'm all Jesused up now, I swear. <<if $Room == .Church>>
=> {$secondary}: I promise I'll never eat {$primary}'s lunch again. <<if $Room == .Office>>
=> {$secondary}: I promise I'll no more cheating at darts. <<if $Room == .Pub>>
===

