// the rescue scenario
// here primary has kidnapped secondary and the player needs to rescue them

// speaking to either primary or secondary starts the scenario
title: Primary
when: $scenario == .Rescue
when: $scenario_state == .NotStarted
---
{$primary}: It's too late to stop me!
Player: How dare you kidnap {$secondary}
{$primary}: But I am evil!
{$secondary}: yeah I mean that checks out.

<<set $scenario_state = .Started>>
===

title: Secondary
when: $scenario == .Rescue
when: $scenario_state == .NotStarted
---
Player: Don't worry {$secondary}, I'll save you!
{$secondary}: oh thanks
{$primary}: hey don't you dare!

<<set $scenario_state = .Started>>
===




// once started you will need to talk to Primary before you can rescue secondary

title: Secondary
when: $scenario == .Rescue
when: $scenario_state == .Started
when: $speak_to_primary == false
---
Player: Ok I am gonna get you out.
{$secondary}: I don't think {$primary} will let you.
Player: oh right.

<<set $speak_to_secondary = true>>
===

title: Secondary
when: $scenario == .Rescue
when: $scenario_state == .Started
when: $speak_to_primary == true
---
Player: Ok I am gonna get you out.
{$secondary}: oh thank you so much.
Player: no worries.

<<set $scenario_state = .Complete>>
===

title: Primary
when: $scenario == .Rescue
when: $scenario_state == .Started
when: $speak_to_primary == false
---
Player: ok you need to let {$secondary} go.
{$primary}: but I am the baddie
Player: yeah but I need this to be resolved.
{$primary}: that sounds like a you problem
Player: look I am just gonna go release {$secondary}.
{$primary}: noooooooo

<<set $speak_to_primary = true>>
===

// if you have already spoken to primary AFTER starting the scenario they will refuse to talk to you anymore
title: Primary
when: $scenario == .Rescue
when: $scenario_state == .Started
when: $speak_to_primary == true
---
{$primary}: I have nothing to say to you.
===





// having now rescued secondary the scenario is complete
title: Primary
when: $scenario == .Rescue
when: $scenario_state == .Complete
---
{$primary}: drat, you defeated me.
===

title: Secondary
when: $scenario == .Rescue
when: $scenario_state == .Complete
---
{$secondary}: Thank you for saving me.
===