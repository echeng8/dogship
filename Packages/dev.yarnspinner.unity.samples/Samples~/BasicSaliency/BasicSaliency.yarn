title: Setup
---
<<enum Day>>
    <<case Monday>>
    <<case Tuesday>>
    <<case Wednesday>>
<<endenum>>

<<enum TimeOfDay>>
    <<case Morning>>
    <<case Evening>>
<<endenum>>

/// the current day
<<declare $day = Day.Monday>>
/// the current time
<<declare $time = TimeOfDay.Morning>>

/// the number of gold coins the player has
<<declare $gold = 0>>
===


title: Alice
when: $day == .Monday
---
Player: Hey
Alice: Hey, it's Monday
Player: yep
Alice: and I only have a single storylet for Monday regardless of the time of day
Player: cool
Alice: indeed
===
title: Alice
when: $day == .Tuesday
when: $time == .Morning
---
Player: Hello
Alice: Hello, what a great Tuesday morning it is!
===
title: Alice
when: $day == .Tuesday
when: $time == .Evening
---
Player: Hello
Alice: Ah Tuesday evening, always the best time of Tuesday I say
===
title: Alice
when: $day == .Tuesday
when: $time == .Evening
---
Player: Hello
Alice: Just between you and me I have some plans this evening.
===

title: Alice
when: $day == .Wednesday
when: $time == .Morning
---
Player: Hello
Alice: Humpday, am I right?
===
title: Alice
when: $day == .Wednesday
when: $time == .Morning
---
Player: Hello
Alice: Ah Wednesday morning, definitely the most morning part of the day.
===

title: Alice
when: $day == .Wednesday
when: $time == .Evening
when: once
---
Player: Hello
Alice: the week is half over, what a relief.
===
title: Alice
when: $day == .Wednesday
when: $time == .Evening
---
Player: Hello
Alice: Probably should go to bed, it is Wednesday evening after all.
===


title: Barry
when: $time == .Morning
---
Player: Hi
Barry: Good morning
===
title: Barry
when: $time == .Morning
---
Player: Hi
Barry: How are you this morning?
===
title: Barry
when: $time == .Morning
---
Player: Hi
Barry: Top of the morning
===

title: Barry
when: $time == .Evening
---
Player: Hi
Barry: Good evening
===
title: Barry
when: $time == .Evening
---
Player: Hi
Barry: What a fine evening it is today
===
title: Barry
when: $time == .Evening
---
Player: Hi
Barry: Wait, is is it evening or afternoon?
===


title: Liz
when: once
when: always
---
Liz: I have this node which will only happen once, neato right?
===
title: Liz
when: always
---
Liz: Oh hi
===
title: Liz
when: always
---
Liz: What up?
===
title: Liz
when: always
---
Liz: Talk to anyone interesting lately?
===


title: George
---
=> George: oh hi
=> George: another Monday, I hate Mondays <<if $day == .Monday>>
=> George: I suppose I should make breakfast <<if $time == .Morning>>
=> George: Weird how tomorrow is Monday, right? <<if $day == .Wednesday && $time == .Evening>>
    Player: did you want the devs to have to write out even more sample content?
    George: it would be nice, yes
    Player: well they aren'that gonna do that
    George: boo
=> George: I wonder if I am in any other samples
===
